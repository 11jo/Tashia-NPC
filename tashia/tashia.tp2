BACKUP ~weidu_external/tashia/backup~
//AUTHOR ~Bert "Lord Ernie" Van Hertum (b.vanhertum@gmail.com)~
SUPPORT ~https://github.com/Spellhold-Studios/Tashia-NPC~
VERSION ~v1.5~

README ~tashia/tra/%LANGUAGE%/readme-tashia.html~
       ~tashia/readme-tashia.html~

ALWAYS
    // EET compatibility
      ACTION_IF GAME_IS ~eet~ BEGIN
        OUTER_SET bg2_chapter = 12
      END ELSE BEGIN
        OUTER_SET bg2_chapter = 0
      END
      OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
        OUTER_SET bg2_chapter = bg2_chapter + 1
        OUTER_SPRINT name_source ~bg2_chapter_%i%~
        OUTER_SET EVAL ~%name_source%~ = bg2_chapter
      END


	//Copies tra files into the autotra-folder (to leave the originals untouched)
	DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
	COPY ~tashia/Languages/english~				~weidu_external/tashia/Languages/autotra/%LANGUAGE%~
	COPY ~tashia/Languages/english/original~				~weidu_external/tashia/Languages/autotra/%LANGUAGE%~
	COPY ~tashia/Languages/english/new~				~weidu_external/tashia/Languages/autotra/%LANGUAGE%~
	COPY ~tashia/Languages/%LANGUAGE%~					~weidu_external/tashia/Languages/autotra/%LANGUAGE%~
	COPY ~tashia/Languages/%LANGUAGE%/original~				~weidu_external/tashia/Languages/autotra/%LANGUAGE%~
	COPY ~tashia/Languages/%LANGUAGE%/new~				~weidu_external/tashia/Languages/autotra/%LANGUAGE%~ IF_EXISTS
	END

	LAF autotra_workaround END

	ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
//  ACTION_DEFINE_ARRAY le#_noconvert BEGIN END

		ACTION_DEFINE_ARRAY le#_reload BEGIN wsetup END

			LAF HANDLE_CHARSETS
				INT_VAR
				infer_charsets = 1
				STR_VAR
				tra_path = EVAL ~weidu_external/tashia/Languages/autotra~
				out_path = EVAL ~weidu_external/tashia/Languages/autotra/UTF8~
				iconv_path = ~tashia/Languages/autotra/iconv~
				// noconvert_array = le#_noconvert
				reload_array = le#_reload
			END

	// UTF8 only
	COPY ~weidu_external/tashia/Languages/autotra/UTF8/%LANGUAGE%~			~weidu_external/tashia/Languages/autotra/%LANGUAGE%~
	END

    ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
      OUTER_TEXT_SPRINT tra_location ~weidu_external/tashia/Languages/autotra~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT tra_location ~tashia/languages~
    END

END

// Compatibility with SoA only
ALLOW_MISSING ~fatesp.dlg~
              ~sengua01.dlg~
              ~hgwra01.dlg~
              ~finsol01.dlg~
              ~sarvolo.dlg~
              ~sarev25j.dlg~
              ~valyg25j.dlg~
              ~vicon25j.dlg~
              ~aerie25j.dlg~
              ~minsc25j.dlg~
              ~imoen25j.dlg~
              ~korga25j.dlg~
              ~ar4500.bcs~
              ~ar6200.bcs~

// Translations
AUTO_TRA ~%tra_location%/%s~

LANGUAGE ~English~
         ~english~
         ~tashia/languages/english/wsetup.tra~

LANGUAGE ~Russian (translation by ArtBersercer, aerie.ru, and yota13)~ // UTF8 should be ANSI
         ~russian~
         ~tashia/languages/english/wsetup.tra~
         ~tashia/languages/russian/wsetup.tra~

LANGUAGE ~Deutsch (Uebersetzung von Maus)~
         ~german~
         ~tashia/languages/english/wsetup.tra~
         ~tashia/languages/german/wsetup.tra~

LANGUAGE ~Espanol (traducido por el Clan DLAN)~
         ~spanish~
         ~tashia/languages/english/wsetup.tra~
         ~tashia/languages/spanish/wsetup.tra~

LANGUAGE ~Francais (traduction de Yoho)~
         ~french~
         ~tashia/languages/english/wsetup.tra~
         ~tashia/languages/french/wsetup.tra~

LANGUAGE ~Italiano (traduzione di al17 e Hellspawn)~
         ~italian~
         ~tashia/languages/english/wsetup.tra~
         ~tashia/languages/italian/wsetup.tra~


// Installation
BEGIN @303 // @0
DESIGNATED 0
LABEL ~le#-Tashia~
// SUBCOMPONENT @303 (GAME_IS ~tob bg2ee eet~) // hide subcomponent on SoA-only
REQUIRE_PREDICATE (GAME_IS ~soa tob bg2ee eet~) @319 // prevent component on non-BG2 games

LAF HANDLE_AUDIO END

APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~

ACTION_IF (NOT GAME_IS ~tob bg2ee eet~) BEGIN
// Voraussetzungen für die Installation
	APPEND ~TRIGGER.IDS~ ~0x40DB InWatchersKeep()~
		UNLESS ~0x40DB InWatchersKeep()~
END

// Impotant things for the installation

// Copy creatures and assign proper strings
COPY ~tashia/Creatures/tashia.cre~ ~override/tashia.cre~
     ~tashia/Creatures/taevil.cre~ ~override/taevil.cre~
  SAY NAME1 @1
  SAY NAME2 @1
  SAY BIO @2
  SAY SELECT_ACTION1 ~~ [tashia_] //0292
  SAY SELECT_COMMON4 ~~ [tashia0] //0280
  SAY TARGET_IMMUNE ~~ [tashia2] //0432
  SAY INVENTORY_FULL ~~ [tashia3] //0436
  SAY PICKED_POCKET ~~ [tashia4] //0440
  SAY EXISTANCE1 ~~ [tashia5] //0444
  SAY EXISTANCE2 ~~ [tashia6] //0448
  SAY BATTLE_CRY1 ~~ [tashiaa] //0200
  SAY LEADER ~~ [tashiab] //0188
  SAY TIRED ~~ [tashiac] //0192
  SAY BORED ~~ [tashiad] //0196
  SAY HURT ~~ [tashiae] //0244
  SAY SELECT_COMMON1 ~~ [tashiaf] //0268
  SAY SELECT_COMMON2 ~~ [tashiag] //0272
  SAY SELECT_COMMON3 ~~ [tashiah] //0276
  SAY INITIAL_MEETING ~~ [tashiah] //0164
  SAY BATTLE_CRY2 ~~ [tashiai] //0204
  SAY BATTLE_CRY3 ~~ [tashiaj] //0208
  SAY BATTLE_CRY4 ~~ [tashiak] //0212
  SAY DAMAGE ~~ [tashial] //0236
  SAY DYING ~~ [tashiam] //0240
  SAY AREA_FOREST ~~ [tashian] //0248
  SAY AREA_CITY ~~ [tashiao] //0252
  SAY AREA_DUNGEON ~~ [tashiap] //0256
  SAY AREA_DAY ~~ [tashiaq] //0260
  SAY AREA_NIGHT ~~ [tashiar] //0264
  SAY SELECT_ACTION2 ~~ [tashias] //0296
  SAY SELECT_ACTION3 ~~ [tashiat] //0300
  SAY SELECT_ACTION4 ~~ [tashiau] //0304
  SAY SELECT_ACTION5 ~~ [tashiav] //0308
  SAY SELECT_RARE1 ~~ [tashiax] //0416
  SAY SELECT_RARE2 ~~ [tashiay] //0420
  SAY CRITICAL_HIT ~~ [tashiaz] //0424

COPY ~tashia/Creatures/taarilis.cre~ ~override/taarilis.cre~
  SAY NAME1 @35
  SAY NAME2 @36

COPY ~tashia/Creatures/tajarmis.cre~ ~override/tajarmis.cre~
  SAY NAME1 @37
  SAY NAME2 @38

COPY ~tashia/Creatures/tavex.cre~ ~override/tavex.cre~
  SAY NAME1 @39
  SAY NAME2 @39

COPY ~tashia/Creatures/tamage.cre~ ~override/tamage.cre~
  SAY NAME1 @40
  SAY NAME2 @40

COPY ~tashia/Creatures/tasold01.cre~ ~override/tasold01.cre~
     ~tashia/Creatures/tasold02.cre~ ~override/tasold02.cre~
  SAY NAME1 @41
  SAY NAME2 @41

COPY ~tashia/Creatures/taelite.cre~ ~override/taelite.cre~
  SAY NAME1 @42
  SAY NAME2 @42

COPY ~tashia/Creatures/tagirl.cre~ ~override/tagirl.cre~
  SAY NAME1 @43
  SAY NAME2 @43

COPY ~tashia/Creatures/tatump.cre~ ~override/tatump.cre~
  SAY NAME1 @44
  SAY NAME2 @44

COPY ~tashia/Creatures/takomt.cre~ ~override/takomt.cre~
  SAY NAME1 @45
  SAY NAME2 @45

COPY ~tashia/Creatures/tadream.cre~ ~override/tadream.cre~
  SAY NAME1 @46
  SAY NAME2 @46

COPY ~tashia/Creatures/tafamil.cre~ ~override/tafamil.cre~
  SAY NAME1 @47
  SAY NAME2 @47

COPY ~tashia/Items/tafamil.itm~ ~override/tafamil.itm~
  SAY NAME1 @48
  SAY NAME2 @48
  SAY DESC @49

ACTION_IF (GAME_IS ~tob bg2ee eet~) BEGIN
	COPY ~tashia/Creatures/tmother.cre~ ~override/tmother.cre~
		SAY NAME1 @50
		SAY NAME2 @50
END

COPY ~tashia/Items/tafamclw.itm~ ~override/tafamclw.itm~
  SAY NAME1 @290
  SAY NAME2 @290


// Copy files
COPY ~tashia/Media/tafamil.eff~ ~override/tafamil.eff~
     ~tashia/Media/j#blank.mus~ ~music/j#blank.mus~

ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
	COPY ~tashia/media/portraits/ee~ ~override~

	COPY_EXISTING ~tashia.cre~ ~override~
	WRITE_ASCII 0x34 ~tashiam~ #8 // small portrait
	WRITE_ASCII 0x3c ~tashial~ #8 // large portrait
END ELSE BEGIN
	COPY ~tashia/media/portraits/bg2~ ~override~
END

// Compile scripts
COMPILE EVALUATE_BUFFER ~tashia/Scripts/taarilis.baf~
//  USING ~~

COMPILE EVALUATE_BUFFER ~tashia/Scripts/tadream.baf~
//  USING ~~

COMPILE EVALUATE_BUFFER ~tashia/Scripts/tafamil.baf~
  USING ~%tra_location%/%s/wsetup.tra~

ACTION_IF (GAME_IS ~tob bg2ee eet~) BEGIN

COMPILE EVALUATE_BUFFER ~tashia/Scripts/tash25.baf~
  USING ~%tra_location%/%s/wsetup.tra~

COMPILE EVALUATE_BUFFER ~tashia/Scripts/tashi25d.baf~
//  USING ~~

END

COMPILE EVALUATE_BUFFER ~tashia/Scripts/tashia.baf~
  USING ~%tra_location%/%s/wsetup.tra~

COMPILE EVALUATE_BUFFER ~tashia/Scripts/cuttas01.baf~
  USING ~%tra_location%/%s/wsetup.tra~

COMPILE EVALUATE_BUFFER ~tashia/Scripts/cuttas02.baf~
  USING ~%tra_location%/%s/wsetup.tra~

COMPILE EVALUATE_BUFFER ~tashia/Scripts/cuttas03.baf~
  USING ~%tra_location%/%s/wsetup.tra~

COMPILE EVALUATE_BUFFER ~tashia/Scripts/cuttas04.baf~
//  USING ~~

COMPILE EVALUATE_BUFFER ~tashia/Scripts/cuttas05.baf~
//  USING ~%tra_location%/%s/wsetup.tra~

COMPILE EVALUATE_BUFFER ~tashia/Scripts/cuttas06.baf~
//  USING ~~

COPY_EXISTING ~AR2602.are~ ~override~
 WRITE_ASCII 0x94 ~AR2602~
BUT_ONLY_IF_IT_CHANGES

EXTEND_BOTTOM ~AR2602.bcs~ ~tashia/Scripts/ar2602.baf~  EVALUATE_BUFFER
//  USING ~~

COMPILE EVALUATE_BUFFER ~tashia/Scripts/tfix.baf~
//  USING ~%tra_location%/%s/wsetup.tra~

// Compile dialogs
COMPILE EVALUATE_BUFFER ~tashia/Dialogues/taevil.d~
  USING ~%tra_location%/%s/taevil.tra~

COMPILE EVALUATE_BUFFER ~tashia/Dialogues/tashia.d~
  USING ~%tra_location%/%s/tashia.tra~

// Missing lines for translation no up to date
	WITH_TRA ~tashia/Languages/english/tashiaj.TRA~
			 ~%tra_location%/%s/tashiaj.TRA~ BEGIN
COMPILE EVALUATE_BUFFER ~tashia/Dialogues/tashiaj.d~
	END
//  USING ~%tra_location%/%s/tashiaj.tra~

COMPILE EVALUATE_BUFFER ~tashia/Dialogues/tashiap.d~
  USING ~%tra_location%/%s/tashiap.tra~

// Missing lines for translation no up to date
	WITH_TRA ~tashia/Languages/english/btashia.TRA~
			 ~%tra_location%/%s/btashia.TRA~ BEGIN
COMPILE EVALUATE_BUFFER ~tashia/Dialogues/btashia.d~
	END
//  USING ~%tra_location%/%s/btashia.tra~

COMPILE EVALUATE_BUFFER ~tashia/Dialogues/tajarmis.d~
  USING ~%tra_location%/%s/tajarmis.tra~

COMPILE EVALUATE_BUFFER ~tashia/Dialogues/taarilis.d~
  USING ~%tra_location%/%s/taarilis.tra~

COMPILE EVALUATE_BUFFER ~tashia/Dialogues/tadream.d~
  USING ~%tra_location%/%s/tadream.tra~

COMPILE EVALUATE_BUFFER ~tashia/Dialogues/takomt.d~
  USING ~%tra_location%/%s/takomt.tra~

COMPILE EVALUATE_BUFFER ~tashia/Dialogues/tafamil.d~
  USING ~%tra_location%/%s/tafamil.tra~

ACTION_IF (GAME_IS ~tob bg2ee eet~) BEGIN
	COMPILE EVALUATE_BUFFER ~tashia/Dialogues/btashi25.d~
		USING ~%tra_location%/%s/btashi25.tra~

	COMPILE EVALUATE_BUFFER ~tashia/Dialogues/tashi25j.d~
		USING ~%tra_location%/%s/tashi25j.tra~

	COMPILE EVALUATE_BUFFER ~tashia/Dialogues/tashi25p.d~
		USING ~%tra_location%/%s/tashi25p.tra~

	COMPILE EVALUATE_BUFFER ~tashia/Dialogues/tmother.d~
		USING ~%tra_location%/%s/tmother.tra~
END

COMPILE EVALUATE_BUFFER ~tashia/Dialogues/taflirt.d~
  USING ~%tra_location%/%s/taflirt.tra~

// Patch StringRefs in scripts

COPY ~tashia/Media/tashend1.2da~ ~override/tashend1.2da~
  REPLACE ~187678~ @103
COPY ~tashia/Media/tashend2.2da~ ~override/tashend2.2da~
  REPLACE ~187678~ @104
COPY ~tashia/Media/tashend3.2da~ ~override/tashend3.2da~
  REPLACE ~187678~ @105

// Pretend scripts
EXTEND_TOP ~player1d.bcs~ ~tashia/Scripts/tashia1d.bcs~ EVALUATE_BUFFER // BAF
EXTEND_TOP ~ar2010.bcs~ ~tashia/Scripts/tash2010.bcs~ EVALUATE_BUFFER // BAF

ACTION_IF (GAME_IS ~tob bg2ee eet~) BEGIN
	EXTEND_TOP ~ar4500.bcs~ ~tashia/Scripts/tash4500.bcs~ EVALUATE_BUFFER // BAF
	EXTEND_TOP ~ar6200.bcs~ ~tashia/Scripts/tash6200.bcs~ EVALUATE_BUFFER // BAF
	EXTEND_BOTTOM ~cut218g.bcs~ ~tashia/Scripts/tcut218g.bcs~ EVALUATE_BUFFER // BAF
END

// Append Tashia NPC dialogs to the games 2da files

COPY_EXISTING ~songlist.2da~ ~override~
SET_2DA_ENTRY 0 2 3 ~J#BLANK.MUS~

APPEND ~pdialog.2da~
  ~TASHIA       TASHIAP            TASHIAJ           TASHIAD~
	UNLESS ~TASHIA~
		UNLESS ~25POST~

APPEND ~pdialog.2da~
  ~TASHIA       TASHIAP            TASHIAJ           TASHIAD        TASHI25P           TASHI25J              TASHI25D              tash25~
	UNLESS ~TASHIA~
		IF ~25POST~

APPEND ~interdia.2da~
  ~TASHIA      BTASHIA~
	UNLESS ~TASHIA~
		UNLESS ~25FILE~

APPEND ~interdia.2da~
  ~TASHIA      BTASHIA       BTASHI25~
	UNLESS ~TASHIA~
		IF ~25FILE~


APPEND ~npclevel.2da~
  ~Tashia      Tashia      Tashia      Tashia      Tashia      Tashia      Tashia     Tashia   Tashia      Tashia      Tashia      Tashia      Tashia      Tashia      Tashia      Tashia      Tashia     Tashia   Tashia   Tashia    Tashia   Tashia    Tashia   Tashia~
	UNLESS ~Tashia~

ACTION_IF (GAME_IS ~tob bg2ee eet~) BEGIN
	APPEND ~npclvl25.2da~
		~Tashia      Tashia      Tashia      Tashia      Tashia      Tashia      Tashia     Tashia   Tashia      Tashia      Tashia      Tashia      Tashia      Tashia      Tashia      Tashia      Tashia     Tashia   Tashia   Tashia    Tashia   Tashia    Tashia   Tashia~
		UNLESS ~Tashia~
END

/* Tashia Remix */

BEGIN @301
DESIGNATED 2
LABEL ~le#-Tashia-Action-Dialogues~
SUBCOMPONENT @300
REQUIRE_PREDICATE (GAME_IS ~tob bg2ee eet~) @319 // prevent component on non-BG2 games
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~tashia.tp2~ (ID_OF_LABEL ~tashia.tp2~ ~le#-Tashia~)) @312 // require full version

			OUTER_SET Action-Dialogues = 1
			OUTER_SET Baldurized-Dialogues = 0

		INCLUDE ~tashia/lib/le#_Dialogues.tph~

BEGIN @302
DESIGNATED 3
 LABEL ~le#-Tashia-Baldurized-Dialogues~
SUBCOMPONENT @300
REQUIRE_PREDICATE (GAME_IS ~tob bg2ee eet~) @319 // prevent component on non-BG2 games
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~tashia.tp2~ (ID_OF_LABEL ~tashia.tp2~ ~le#-Tashia~)) @312 // require full version

			OUTER_SET Baldurized-Dialogues = 1
			OUTER_SET Action-Dialogues = 0

		INCLUDE ~tashia/lib/le#_Dialogues.tph~


BEGIN @316
DESIGNATED 4
LABEL ~le#-Tashia-Alt-Portrait-1~
SUBCOMPONENT @318
REQUIRE_PREDICATE (GAME_IS ~soa tob bg2ee eet~) @319 // prevent component on non-BG2 games
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~tashia.tp2~ (ID_OF_LABEL ~tashia.tp2~ ~le#-Tashia~)) @320 // require main component

ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
	COPY ~tashia/update/portraits/alternative1/ee~  ~override~
END ELSE BEGIN
	COPY ~tashia/update/portraits/alternative1/bg2~ ~override~
END

BEGIN @317
DESIGNATED 5
LABEL ~le#-Tashia-Alt-Portrait-2~
SUBCOMPONENT @318
REQUIRE_PREDICATE (GAME_IS ~soa tob bg2ee eet~) @319 // prevent component on non-BG2 games
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~tashia.tp2~ (ID_OF_LABEL ~tashia.tp2~ ~le#-Tashia~)) @320 // require main component

ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
	COPY ~tashia/update/portraits/alternative2/ee~  ~override~
END ELSE BEGIN
	COPY ~tashia/update/portraits/alternative2/bg2~ ~override~
END
